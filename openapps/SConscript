import os

Import('env')

localEnv = env.Clone()

target = 'libopenapps'
sources_c = [
    os.path.join('openapps.c'),
    os.path.join('c6t',           'c6t.c'),
    os.path.join('cexample',      'cexample.c'),
    os.path.join('cinfo',         'cinfo.c'),
    os.path.join('cleds',         'cleds.c'),
    os.path.join('cstorm',        'cstorm.c'),
    os.path.join('cwellknown',    'cwellknown.c'),
    os.path.join('techo',         'techo.c'),
    os.path.join('tohlone',       'tohlone.c'),
    os.path.join('tohlone',       'tohlone_webpages.c'),
    os.path.join('uecho',         'uecho.c'),
]
sources_h = [
    os.path.join('openapps.h'),
    os.path.join('c6t',           'c6t.h'),
    os.path.join('cexample',      'cexample.h'),
    os.path.join('cinfo',         'cinfo.h'),
    os.path.join('cleds',         'cleds.h'),
    os.path.join('cstorm',        'cstorm.h'),
    os.path.join('cwellknown',    'cwellknown.h'),
    os.path.join('techo',         'techo.h'),
    os.path.join('tohlone',       'tohlone.h'),
    os.path.join('tohlone',       'tohlone_webpages.h'),
    os.path.join('uecho',         'uecho.h'),
]

if localEnv['board']=='python':
    
    for s in sources_c+sources_h:
        temp = localEnv.Objectify(
            target = localEnv.ObjectifiedFilename(s),
            source = s,
        )
        #localEnv.AlwaysBuild(temp)
    
    libopenapps = localEnv.Library(
        target = target,
        source = [localEnv.ObjectifiedFilename(s) for s in sources_c],
    )
    localEnv.Depends(libopenapps,[localEnv.ObjectifiedFilename(s) for s in sources_h])
    
else:
    
    localEnv.Append(
        CPPPATH =  [
            # inc
            os.path.join('#','inc'),
            # openapps
            os.path.join('#','openapps'),
            os.path.join('#','openapps','c6t'),
            os.path.join('#','openapps','cexample'),
            os.path.join('#','openapps','cinfo'),
            os.path.join('#','openapps','cleds'),
            os.path.join('#','openapps','cstorm'),
            os.path.join('#','openapps','cwellknown'),
            os.path.join('#','openapps','techo'),
            os.path.join('#','openapps','tohlone'),
            os.path.join('#','openapps','uecho'),
            # openstack
            os.path.join('#','openstack','04-TRAN'),
            os.path.join('#','openstack','cross-layers'),
            os.path.join('#','openstack','02a-MAClow'),
            os.path.join('#','openstack','02b-MAChigh'),
            os.path.join('#','openstack','03b-IPv6'),
        ],
    )
    
    libopenapps = localEnv.Library(
        target = target,
        source = sources_c,
    )

Alias('libopenapps', libopenapps)
