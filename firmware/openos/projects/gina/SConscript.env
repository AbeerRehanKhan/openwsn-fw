import os

Import('env')

# create build environment
buildEnv = env.Clone()

# inherit environment from user (PATH, etc)
buildEnv['ENV'] = os.environ

# choose bsp. Normally this would be the same as the board name,
# however, there are cases where one might want to make separate build
# configuration for the same board.
buildEnv['BSP'] = buildEnv['board']

# include board/bsp-specific directories
buildEnv.Append(
   CPPPATH = [
      os.path.join('#','firmware','openos','bsp','boards',env['board']),
   ]
)

MSPVERSION = "2618"
if   env['toolchain']=='mspgcc':
   flags = []
   flags += ['-mmcu=msp430f{0}'.format(MSPVERSION)]
   flags += ['-Os']
   buildEnv.Append(CCFLAGS   = ' '.join(flags))
   buildEnv.Append(LINKFLAGS = ' '.join(flags))
elif env['toolchain']=='iar':
   env.Append(CCFLAGS        = '-D__MSP430F{0}__'.format(MSPVERSION))
   env.Append(LINKFLAGS      = '-f "C:\\Program Files\\IAR Systems\\Embedded Workbench 6.4\\430\\CONFIG\\lnk430F{0}.xcl"'.format(MSPVERSION))

   
Return('buildEnv')
